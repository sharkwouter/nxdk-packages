pkgname=jsoncpp
pkgver=1.9.5
pkgrel=2
pkgdesc="a C++ library for interacting with JSON"
arch=('i686')
license=('unlicense')
url="https://github.com/open-source-parsers/jsoncpp"
makedepends=()
source=("git+https://github.com/open-source-parsers/jsoncpp#commit=5defb4ed1a4293b8e2bf641e16b156fb9de498cc")
sha256sums=('SKIP')

prepare() {
    cd "$pkgname"
    sed -i 's#@CMAKE_INSTALL_PREFIX@#${NXDK_DIR}#g' pkg-config/jsoncpp.pc.in
    sed -i 's#@libdir_for_pc_file@#${prefix}/lib#' pkg-config/jsoncpp.pc.in
    sed -i 's#@includedir_for_pc_file@#${prefix}/include#' pkg-config/jsoncpp.pc.in

    sed -i 's#JSON_USE_EXCEPTION 1#JSON_USE_EXCEPTION 0#'  include/json/config.h
}

build() {
  cd $pkgname
  mkdir -p build
  cd build
  nxdk-cmake \
    -DCMAKE_INSTALL_PREFIX=${pkgdir} -DCMAKE_BUILD_TYPE=Release \
    -DJSONCPP_WITH_POST_BUILD_UNITTEST=OFF -DJSONCPP_WITH_TESTS=OFF \
    -DBUILD_SHARED_LIBS=OFF ..
  make
}

package () {
  cd "$pkgname/build"
  make install
  cd ..

  mkdir -m 755 -p "$pkgdir/share/licenses/$pkgname"
  install -m 644 LICENSE "$pkgdir/share/licenses/$pkgname"
}
